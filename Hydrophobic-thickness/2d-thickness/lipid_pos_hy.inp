* GENERATED BY CHARMM-GUI (http://www.charmm-gui.org) on Feb, 23. 2012.
* INPUT FILES FOR EQUILIBRATION
*

!! extrenal parameters
!! run : system id
!! cntmin: start run to analyze
!! cntmax: final run to analze

! set wdir = /share/ceph/woi216group/shared/md_cav1/run@{run} ! working dir

set pi  = acos (-1.0) ! 3.14159265
calc dtr = @pi / 180.0
calc rtd = 180.0 / @pi

! Read topology and parameter files
stream toppar_charmm.str

! Read PSF and Coordinates
open read unit 10 card name step5_charmm2omm.psf
read psf  unit 10 card

calc icnt = 0 ! initial counter

! Read Coordinates
open read unit 10 card name step5_charmm2omm.crd
read coor unit 10 card

stream ../step5_assembly.str

!
! Image Setup
!

!! read current coordinates from restart file
!open read unit 10 card name step6.6_equilibration.crd
!read coor unit 10 card
!                           !read coor dynr curr unit 10
!
!stream junk.str
!
!set boxtype = rect
!set xtltype = tetragonal
!set zcen    = 0.0
!
!system "python checkfft.py @A @B @C > checkfft.str"
!stream checkfft.str

open read unit 10 card name crystal_image.str
CRYSTAL DEFINE @XTLtype @A @B @C 90.0 90.0 90.0
CRYSTAL READ UNIT 10 CARD

!Image centering by residue
IMAGE BYRESID XCEN 0.0 YCEN 0.0 ZCEN @zcen sele resname TIP3 .or. segid MEMB end
IMAGE BYRESID XCEN 0.0 YCEN 0.0 ZCEN @zcen sele resname POT .or. resname CLA end
IMAGE BYSEGID XCEN 0.0 YCEN 0.0 ZCEN @zcen sele segid PROA end

!
! Nonbonded Options [short distances and no pme just for image centering]
!

nbonds atom vatom vfswitch bycb -
       ctonnb 4.0 ctofnb 5.0 cutnb 6.0 cutim 6.0 -
       inbfrq -1 imgfrq -1 wmin 1.0 cdie eps 1.0 -
       ! ewald pmew fftx @fftx ffty @ffty fftz @fftz  kappa .34 spline order 6
energy

! TM1 and TM2
define TM  sele segid PROA .and. resid 98:131 .and. type CA end
define AH1 sele segid PROA .and. resid 62:67 .and. type CA end
define AH2 sele segid PROA .and. resid 76:85 .and. type CA end

define BB sele segid PRO* .and. ( type C .or. type N .or. type CA .or. type O ) end
define HA sele segid PRO* .and. .not. type H* end

coor stat sele segid MEMB .and. type P end
set nlipids = ?nsel

coor stat sele HA end
set nhatom = ?nsel

!
! Trajectory analysis
!

calc cnt = @{cntmin}
calc cntmax = @{cntmax}
calc tcnt = 1 

!! write output for analysis of conformation
open write card unit 51 name lipid_ifitm_top_@{cntmax}.plo
open write card unit 52 name lipid_ifitm_bot_@{cntmax}.plo
open write card unit 53 name lipid_ifitm_hapos_@{cntmax}.plo

label opentrj
  open read unit 11 file name step7_@{cnt}.dcd ! @{wdir}/md_@{cnt}.dcd
  traj query unit 11
  traj iread 11 nread 1 skip 1

  calc nfile = ?NFILE
  calc nfile = @nfile - 1
  calc cmt = 1

  label readtrj
    traj read

    ! bilayer-recentering
    coor stat sele segid MEMB .and. resid 1 end
    coor trans zdir -?zave sele all end
    energy
    coor stat sele segid MEMB .and. ( .not. type H* ) end
    coor trans zdir -?zave sele all end
    energy

    ! align ifitm
    stream align_ifitm.str

    ! calc time = @cmt * 0.5
    calc time = @cmt * 0.01 + @cnt - 1 ! ns

    ! do extract the coordinates of cav1 heavy atoms & lipid phosphate coordinates..
    set ilip = 1
    label dolip 
      coor stat sele segid MEMB .and. resid @ilip .and. type P end
      set wuni = 52
      if ?zave .gt. 0 then
        set wuni = 51
      endif

      write title unit @wuni
      * @time @ilip ?xave ?yave ?zave
      *
      incr ilip by 1
    if ilip .le. @nlipids goto dolip

    set iha = 1
    label doha
      coor stat sele HA .subset. @iha end
      write title unit 53
      * @time @iha ?xave ?yave ?zave
      *
      incr iha by 1
    if iha .le. @nhatom goto doha

    !! stop

    incr cmt by 1
  if cmt .le. @nfile goto readtrj
  incr cnt by 1
if cnt .le. @cntmax goto opentrj

stop

