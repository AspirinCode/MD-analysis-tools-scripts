#!/bin/gnuplot

# plot rmsd map
set term postscript eps enhanced solid color "Helvetica,30"
set output "thickness_2d_ifitm_pal_1st_500ns.eps"

# set term x11 enhanced font "Helvetica-Bold,30"
set encoding iso_8859_1

set title "500ns" offset 1.4,0.0 font "Helvetica-Bold,25"

set size square # ratio 0.62
set border lw 3

set xrange[-35:35]
set yrange[-35:35]
set xtics 10
set ytics 10

# generate contour of Cav1 density
f_in='./lipid_ifitm_prot.density'
s_in=sprintf("< awk '{if (prev!=$1) print \"\"; print $0; prev=$1}' %s", f_in)

set contour
set cntrparam cubicspline
set cntrparam levels 4
set cntrparam levels incremental 0.25,0.5
unset clabel
unset surface
set table 'tmp.cnt'
splot s_in u 1:2:($3/$4)
unset table

# Change single blank lines to double blank lines
!awk "NF<2{printf\"\n\"}{print}" < ./tmp.cnt > ./lipid_ifitm_prot.cnt
#
#set cbrange[CBMIN:CBMAX]
#set cbtics CBTICS
set cbrange[20:44]
set cbtics 4

#unset colorbox
# set format y " "

set palette defined (0 "white", 1.e-10 "blue", 2 "green", 3 "yellow", 4 "orange", 5 "red")

plot './lipid_ifitm_bilayer.prof' u 1:2:3 w image notitle, \
     './lipid_ifitm_prot.cnt' w l lw 3 lc rgb "black" notitle
